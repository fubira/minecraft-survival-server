# KenmoMinecraftClub SurvivaRoyal support script

options:
  HEADER: <gray>[<dark green>SurvivaRoyal<gray>]<reset>
  WORLD: world

variables:
  {running} = 0
  {teamchat} = 0
  {progress} = 0
  {nextstar} = 0

on script load:
  set {teamcolor.red} to "light red"
  set {teamcolor.green} to "light green"
  set {teamcolor.blue} to "light blue"
  set {tmsghead.red} to "<gray>[<yellow>-> TO TEAM <light red>RED<gray>]<reset>"
  set {tmsghead.green} to "<gray>[<yellow>-> TO TEAM <light green>GREEN<gray>]<reset>"
  set {tmsghead.blue} to "<gray>[<yellow>-> TO TEAM <light blue>BLUE<gray>]<reset>"
  set {tchathead.red} to "<gray>[<gold>TEAM <light red>RED<gray>]<reset>"
  set {tchathead.green} to "<gray>[<gold>TEAM <light green>GREEN<gray>]<reset>"
  set {tchathead.blue} to "<gray>[<gold>TEAM <light blue>BLUE<gray>]<reset>"


#
# サバイバロイヤルコマンド

command /sr <text>:
  description: SurvivaRoyal control command
  permission: survivaroyal.control
  aliases: /surv
  usage: /sr <command>
  trigger:
    set {_cmd} to "%arg 1%"

    if {_cmd} is "reload":
      execute console command "/skript reload SurvivaRoyal.sk"

    else if {_cmd} is "start":
      if {running} is 1:
        execute command "/sr info"
      else:
        set {running} to 1
        set {progress} to 0
        set {nextstar} to 0
        set time of {@WORLD} to 6:00
        broadcast "{@HEADER} <light red>*** <yellow>サバイバロイヤルを開始します <light red>***<reset>"

    else if {_cmd} is "stop":
      if {running} is 1:
        set {running} to 0
        broadcast "{@HEADER} <light red>*** <yellow>サバイバロイヤルを終了します <light red>***<reset>"
        execute command "/sr score"
      else:
        message "{@HEADER} サバイバロイヤルは開催されていません"

    else if {_cmd} is "info":
      if {running} is 1:
        broadcast "{@HEADER} サバイバロイヤルは<gold>進行中<reset>です <gray>(<light gray>%{progress}%分経過<gray>)"
        execute command "/sr score"
      else:
        message "{@HEADER} サバイバロイヤルは開催されていません"

    else if {_cmd} is "score":
      set {_red_members} to 0
      set {_red_point} to 0
      set {_green_members} to 0
      set {_green_point} to 0
      set {_blue_members} to 0
      set {_blue_point} to 0

      loop all players:
        if {team.%loop-player%} is "red":
          add amount of nether star in the loop-player's inventory to {_red_point}
          add 1 to {_red_members}

        if {team.%loop-player%} is "green":
          add amount of nether star in the loop-player's inventory to {_green_point}
          add 1 to {_green_members}

        if {team.%loop-player%} is "blue":
          add amount of nether star in the loop-player's inventory to {_blue_point}
          add 1 to {_blue_members}

      broadcast "{@HEADER} <yellow>生存者数<reset> ... <light red>赤<gray>[<white>%{_red_members}%<gray>] <light blue>青<gray>[<white>%{_blue_members}%<gray>] <light green>緑<gray>[<white>%{_green_members}%<gray>]"
      broadcast "{@HEADER} <light cyan>ポイント<reset> ... <light red>赤<gray>[<white>%{_red_point}%<gray>] <light blue>青<gray>[<white>%{_blue_point}%<gray>] <light green>緑<gray>[<white>%{_green_point}%<gray>]"

    else if {_cmd} is "give":
      if {running} is 1:
        loop all players:
          add loop-player to {_%{team.%loop-player%}%::*}

        set {_red random} to a random player out of {_red::*}
        if {_red random} is set:
          broadcast "{@HEADER} <light red>%{_red random}%<reset>の足元に<light cyan>ネザースター<reset>が落ちてきた"
          drop a nether star at {_red random}

        set {_green random} to a random player out of {_green::*}
        if {_green random} is set:
          broadcast "{@HEADER} <light green>%{_green random}%<reset>の足元に<light cyan>ネザースター<reset>が落ちてきた"
          drop a nether star at {_green random}

        set {_blue random} to a random player out of {_blue::*}
        if {_blue random} is set:
          broadcast "{@HEADER} <light blue>%{_blue random}%<reset>の足元に<light cyan>ネザースター<reset>が落ちてきた"
          drop a nether star at {_blue random}

      else:
        message "{@HEADER} サバイバロイヤルは開催されていません"

    else:
      message "{@HEADER} /sr [reload|start|stop|info|score|give|tcon|tcoff]"

#
# チームに参加する

command /srjoin <colour> [<player>]:
  description: Join to SurvivaRoyal team
  usage: /srjoin <team> [<player>]
  permission: survivaroyal.control
  trigger:
    set {_team} to "%arg 1%"

    if arg 2 is not set:
      set {_player} to player
    else:
      set {_player} to "%arg 2%"

    if {_team} is "red" or "green" or "blue":
      set {team.%{_player}%} to {_team}
      execute command "/ct add %{team.%{_player}%}% %{_player}%"

    if {_team} is "red":
      broadcast "{@HEADER} %{_player}%が<light red>赤チーム<reset>に参加しました"
    else if {_team} is "green":
      broadcast "{@HEADER} %{_player}%が<light green>緑チーム<reset>に参加しました"
    else if {_team} is "blue":
      broadcast "{@HEADER} %{_player}%が<light blue>青チーム<reset>に参加しました"
    else:
      message "{@HEADER} <light red>チーム指定が不正です<reset>"

#
# チームから離れる

command /srleave [<text>]:
  description: Leave from SurvivaRoyal team
  usage: /srleave [<player>]
  permission: survivaroyal.control
  trigger:
    if arg 1 is not set:
      set {_player} to player
    else:
      set {_player} to "%arg 1%"

    if {team.%{_player}%} is not set:
      stop

    execute command "/w %{_player}% チームから離脱しました。おつかれさまでした"
    execute command "/ct leave %{_player}%"
    delete {team.%{_player}%}

#
# 対チームメッセージ送信

command /teammsg <colour> <text>:
  description: Send message to team
  usage: /teammsg <team> <message>
  permission: survivaroyal.control
  aliases: /tmsg, /tm
  trigger:
    set {_team} to "%arg 1%"
    if player is set:
      set {_sender} to player
      set {_sender_team} to {team.%player%}
      if {_sender_team} is set:
        set {_sender_color} to {teamcolor.%{team.%player%}%}
      else:
        set {_sender_color} to "white"
      set {_sender_tag} to "<<%{_sender_color}%>%{_sender}%<reset>>"
    else:
      set {_sender} to "CONSOLE"
      delete {_sender_team}
      set {_sender_color} to "white"
      set {_sender_tag} to ""
    


    loop all players:
      if {team.%loop-player%} is "%{_team}%":
        message "%{tmsghead.%{_team}%}% %{_sender_tag}% %arg 2%" to loop-player
    if {_sender} is player:
      if {_team} is not "%{_sender_team}%":
        message "%{tmsghead.%{_team}%}% %{_sender_tag}% %arg 2%" to player
    message "%{tmsghead.%{_team}%}% %{_sender_tag}% %arg 2%" to console 

#
# チームチャット

command /tchat [<boolean>]:
  description: Teamchat activate / deactivate
  usage: /tchat [on | off]
  permission: survivaroyal.control
  aliases: /tc
  trigger:
    set {_arg} to "%arg 1%"
    if {_arg} is "true":
      set {teamchat} to 1
      message "<gray>チームチャットを有効にしました<reset>"
    else if {_arg} is "false":
      set {teamchat} to 0
      message "<gray>チームチャットを無効にしました<reset>"
    else:
      if {teamchat} is 0:
        message "<gray>チームチャットは無効です<reset>"
      else:
        message "<gray>チームチャットは有効です<reset>"


on chat:
  if {teamchat} is 1:
    if first 3 character of the message is not "<light green>>":
      if {team.%player%} is not set:
        # 観客チャット
        loop all players:
          if {team.%loop-player%} is not set:
            message "<dark aqua>[SPEC]<reset> <%player%> %message%" to loop-player
        message "<dark aqua>[SPEC]<reset> <%player%> %message%" to console
        cancel event

      if {team.%player%} is set:
        # チームチャット
        loop all players:
          if {team.%loop-player%} is "%{team.%player%}%":
            message "%{tchathead.%{team.%player%}%}% <%player%> %message%" to loop-player
          if {team.%loop-player%} is not set:
            message "%{tchathead.%{team.%player%}%}% <%player%> %message%" to loop-player
        message "%{tchathead.%{team.%player%}%}% <%player%> %message%" to console
        cancel event


#
# 経過時間のカウント

every 1 minutes:
  if {running} is 1:
    add 1 to {progress}

    # 開始から10分おきに経過時間を表示する
    set {_p} to {progress}
    while {_p} >= 10:
      add -10 to {_p}
    if {_p} is 0:
      broadcast "{@HEADER} 開始から<gold>%{progress}%分<reset>が経過しました"

    # 5～10分程度経過したらスターを配布
    add 1 to {nextstar}

    if {nextstar} >= 10:
      set {nextstar} to random integer between 0 and 5
      #broadcast "{@HEADER} <gold>空から<light cyan>星<gold>が降り注いだ… "
      #execute command "/sr give"


#
# 定期的にスコアを表示する

every 5 minutes:
  if {running} is 1:
    execute command "/sr score"

#
# 各プレイヤーのコンパスのターゲットを最も近いベッドに設定する

every 5 seconds in "{@WORLD}":
  if {running} is 1:
    loop all players:
      set {_distance} to 99999
      delete {_target}

      if {bed.red} is set:
        set {_d_red} to distance between the loop-player and {bed.red} 
        if {_distance} is larger than {_d_red}:
          set {_distance} to {_d_red}
          set {_target} to {bed.red}
        
      if {bed.green} is set:
        set {_d_green} to distance between the loop-player and {bed.green}
        if {_distance} is larger than {_d_green}:
          set {_distance} to {_d_green}
          set {_target} to {bed.green}
        
      if {bed.blue} is set:
        set {_d_blue} to distance between the loop-player and {bed.blue}
        if {_distance} is larger than {_d_blue}:
          set {_distance} to {_d_blue}
          set {_target} to {bed.blue}

      if {_target} is set:
        set the loop-player's compass target to {_target}

      #message "%location of loop-player% %{_distance}% %{bed.red}% %{_target}%" to console 
#
# ゲーム参加者がリログすると名前の色が消えるのでログイン時に前のチームに入り直す

on join:
  if {running} is 1:
    if {team.%player%} is set:
      execute command "/ct add %{team.%player%}% %player%"

#
# ベッドの範囲指定、設置チームの記録

#on place of a bed:
#  world is "{@WORLD}":
#    block's y-coordinate is smaller than 65:
#      message "{@HEADER} <light red>y65より下でのベッドの設置はできません<reset>"
#      cancel event
#    block's y-coordinate is larger than 165:
#      message "{@HEADER} <light red>y165より上でのベッドの設置はできません<reset>"
#      cancel event
#
#    # 自チームのベッドが既に存在している場合、警告
#    if {bed.%{team.%player%}%} is set:
#      if player has permission "survivaroyal.control":
#        message "{@HEADER} <yellow>チームベッドを上書きしました。古いベッドが残っている可能性があります。%{bed.%{team.%player%}%}%<reset>"

#      else:
#        message "{@HEADER} <yellow>チームベッドは既に設置済みです。<reset>"
#        cancel event

    # 各チームのメンバーが置いたベッドの座標を覚えておく
#    set {bed.%{team.%player%}%} to block's location


#
# Bedを拾ったことを周知する

on pick up of a bed:
  set {_player} to player
  set {_team} to {team.%player%}

#  world is "{@WORLD}":
#    message "{@HEADER} <yellow>ベッドを拾いました！<reset>"
#    execute command "/tm %{_team}% <yellow>%{_player}%がベッドを拾いました！<reset>"


#
# ベッドの破壊判定

on break of a bed:
  set {_player} to player
  set {_team} to {team.%player%}
#  broadcast "{@HEADER} <yellow>どこかでベッドが破壊されました<reset>"

  #if damage was caused by block explosion:
  #  message "{@HEADER} TNT!TNT!"

#  world is "{@WORLD}":
#    distance between block's location and {bed.red} is smaller than 2:
#      delete {bed.red}
#      {_team} is not "red":
#        execute command "/tm %{_team}% %{_player}%が<light red>赤チーム<reset>のベッドを破壊した！"
#    distance between block's location and {bed.green} is smaller than 2:
#      delete {bed.green}
#      {_team} is not "green":
#        execute command "/tm %{_team}% %{_player}%が<light green>緑チーム<reset>のベッドを破壊した！"
#    distance between block's location and {bed.blue} is smaller than 2:
#      delete {bed.blue}
#      {_team} is not "blue":
#        execute command "/tm %{_team}% %{_player}%が<light blue>青チーム<reset>のベッドを破壊した！"

#on burn of a bed:
#  broadcast "{@HEADER} <yellow>どこかでベッドが燃えました<reset>"

#
# リスポーン時にベッドがなければチームから抜ける

#on respawn:
#  wait 1 tick
#  if {team.%player%} is set:
#    if {bed.%{team.%player%}%} is not set:
#      execute command "/srleave %player%"


#
# Bedクラフト禁止

on craft of a bed:
  world is "{@WORLD}":
    message "{@HEADER} <light red>ベッドのクラフトは禁止されています<reset>"
    cancel event

#
# TNTの爆発威力を増大させる

on explosion:
  entity is tnt:
    make an explosion with power 6 at entity

#on place of tnt:
#  set event-block to air
#  spawn primed tnt at event-location

#on rightclick:
#  if clicked block is tnt:
#    if player is not sneaking:
#      set event-block to air
#      spawn primed tnt at event-location
#      cancel event
  
on projectile hit:
  if projectile is bottle of enchanting:
    create explosion of power 3 at the projectile 
  if projectile is egg:
    create explosion of power 1 at the projectile 

#
# Hopperクラフト禁止

on craft of a hopper:
  world is "{@WORLD}":
    message "{@HEADER} <light red>ホッパーのクラフトは禁止されています<reset>"
    cancel event

on place of a hopper:
  world is "{@WORLD}":
    message "{@HEADER} <light red>ホッパーの設置は禁止されています<reset>"
    cancel event

#
# Item frameクラフト禁止

on craft of a item frame:
  world is "{@WORLD}":
    message "{@HEADER} <light red>アイテムフレームのクラフトは禁止されています<reset>"
    cancel event

on place of a item frame:
  world is "{@WORLD}":
    message "{@HEADER} <light red>アイテムフレームの設置は禁止されています<reset>"
    cancel event
