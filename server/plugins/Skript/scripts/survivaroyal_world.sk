options:
  HEADER: <gray>[<dark green>SurvivaRoyal<gray>]<reset>
  SERVER_ROOT_DIR: /data
  TEMPLATE_DIR: template
  WORLD_DIR: world

variables:
  {world_execute_command} = ""
  {world_execute_param}   = ""

function loadWorld(player: player, command: string, param: string):
  broadcast "{@HEADER} World [<yellow>%{_param}%<reset>] を復元します"
  set {_template_dir} to "{@TEMPLATE_DIR}/%{_param}%"
  set {_template_yml} to "{@TEMPLATE_DIR}/%{_param}%.yml"
  if skutil file {_template_yml} exists:
    set {_border_center} to skutil yaml value "border.center" from file {_template_yml}
    if {_border_center} is not "spawn":
      set {_border_center_x} to skutil yaml value "border.center.x" from file {_template_yml}
      set {_border_center_z} to skutil yaml value "border.center.z" from file {_template_yml}
    set {_border_radius_x} to skutil yaml value "border.radius_x" from file {_template_yml}
    set {_border_radius_z} to skutil yaml value "border.radius_z" from file {_template_yml}
    if skutil yaml "point" in file {_template_yml} does not exist:
      broadcast "{@HEADER} <light red>WARN: チームスポーン地点が未指定です。"
    else:
      set {_point::*} to skutil yaml nodes "point" from file {_template_yml}


  execute command "/dynmap cancelrender world"
  execute command "/mv delete {@WORLD_DIR}"
  execute command "/mv confirm"
  copy dir "%{_template_dir}%" to "/"
  rename dir "/%{_param}%" to "/world"
  execute command "/mv import {@WORLD_DIR} normal"
  execute command "/dynmap fullrender {@WORLD_DIR}"
  
  if {_border_center} is not set:
    execute command "/wb {@WORLD_DIR} clear"
    broadcast "{@HEADER} <light red>WARN: ボーダーが未指定です。"
  else:
    if {_border_center} is "spawn":
      execute command "/wb {@WORLD_DIR} set %{_border_radius_x}% %{_border_radius_z}% spawn"
      broadcast "{@HEADER} Border (spawn) -> [%{_border_radius_x}%, %{_border_radius_z}%]"
    else:
      execute command "/wb {@WORLD_DIR} set %{_border_radius_x}% %{_border_radius_z}% %{_border_center_x}% %{_border_center_z}% "
      broadcast "{@HEADER} Border (%{_border_center_x}%, %{_border_center_z}%) -> [%{_border_radius_x}%, %{_border_radius_z}%]"

  if {_point::*} is set:
    loop {_point::*}:
      set {_pname} to loop-value
      set {_x} to skutil yaml value "point.%{_pname}%.x" from file {_template_yml}
      set {_y} to skutil yaml value "point.%{_pname}%.y" from file {_template_yml}
      set {_z} to skutil yaml value "point.%{_pname}%.z" from file {_template_yml}
      execute command "/ptset %{_pname}% {@WORLD_DIR} %{_x}% %{_y}% %{_z}%"
    broadcast "{@HEADER} Pointが設定されました"
    execute {_player} command "/ptlist"

  broadcast "{@HEADER} World [<yellow>%{_param}%<reset>] が復元されました"

function saveWorld(player: player, command: string, param: string):
  if skutil dir "{@TEMPLATE_DIR}" doesn't exist:
    create directory "{@TEMPLATE_DIR}"

  broadcast "{@HEADER} World [<yellow>%{_param}%<reset>] をテンプレートとして保存します"
  run bash command "cp -rf /data/world /data/{@TEMPLATE_DIR}/%{_param}%"
  broadcast "{@HEADER} World [<yellow>%{_param}%<reset>] を保存しました"

function deleteWorld(player: player, command: string, param: string):
  delete dir "{@TEMPLATE_DIR}/%{_param}%"
  broadcast "{@HEADER} World [<yellow>%{_param}%<reset>] を削除しました"

function clearCommand():
  set {world_execute_command} to ""
  set {world_execute_param} to ""

function reserveCommand(player: player, command: string, param: string, action: string):
  set {world_execute_command} to {_command} 
  set {world_execute_param} to {_param} 
  send "{@HEADER} <light green>World [<yellow>%{_param}%<light green>] を%{_action}%してもよろしいですか?" to {_player}
  send "{@HEADER} <light green>問題なければ <gold>/world confirm <light green>を実行してください" to {_player}


#
# ワールド管理コマンド
command /world [<text>] [<text>]:
  description: SurvivaRoyal World Template Manager
  permission: survivaroyal.control
  usage: /world <command> [<param>]
  trigger:
    set {_cmd} to the 1st argument
    set {_param} to the 2nd argument

    if {_cmd} is "list":
      set {_dir::*} to files in dir "{@TEMPLATE_DIR}"
      set {_templates} to ""
      loop {_dir::*}:
        set {_template} to loop-value
        replace all "{@SERVER_ROOT_DIR}/{@TEMPLATE_DIR}/" in {_template} with ""
        if skutil file "{@TEMPLATE_DIR}/%{_template}%" is a directory:
          if {_templates} is "":
            set {_templates} to {_template}
          else:
            set {_templates} to "%{_templates}%, %{_template}%"

      send "{@HEADER} World: <yellow>%{_templates}%" to player

    else if {_cmd} is "confirm":
      if {world_execute_command} is "load":
        loadWorld(player, {world_execute_command}, {world_execute_param})

      else if {world_execute_command} is "save":
        saveWorld(player, {world_execute_command}, {world_execute_param})

      else if {world_execute_command} is "delete":
        deleteWorld(player, {world_execute_command}, {world_execute_param})

      else:
        if {world_execute_command} is "":
          message "{@HEADER} <light red>コマンドが指定されていません"
        else:
          message "{@HEADER} <light red>不正なコマンドです: <gold>%{world_execute_command}%"
      clearCommand()

    else if {_cmd} is "load":
      if skutil dir "{@TEMPLATE_DIR}/%{_param}%" doesn't exist:
        send "{@HEADER} World [<yellow>%{_param}%<reset>] は存在しません" to {_player}
        exit
      reserveCommand(player, {_cmd}, {_param}, "復元")

    else if {_cmd} is "save":
      if skutil dir "{@TEMPLATE_DIR}/%{_param}%" exists:
        send "{@HEADER} World [<yellow>%{_param}%<reset>] はすでに存在しています。先に<gold> /world delete %{_param}%<white>を実行してください" to {_player}
        exit
      if {_param} is "world":
        send "{@HEADER} テンプレート名は <yellow>world<reset> 以外の名前にしてください" to {_player}
        exit
      reserveCommand(player, {_cmd}, {_param}, "保存")

    else if {_cmd} is "delete":
      if skutil dir "{@TEMPLATE_DIR}/%{_param}%" doesn't exist:
        send "{@HEADER} World [<yellow>%{_param}%<reset>] は存在しません" to {_player}
        exit
      reserveCommand(player, {_cmd}, {_param}, "削除")

    else:
      message "{@HEADER} Usage: world"
      message "<gold>/world list"
      message "<white>  保存されているワールドの一覧を表示します"
      message "<gold>/world load [template_name]"
      message "<white>  指定したワールドを world として復元します"
      message "<gold>/world save [template_name]"
      message "<white>  現在の world をワールドとして保存します"
      message "<gold>/world delete [template_name]"
      message "<white>  指定したワールドを削除します"
    