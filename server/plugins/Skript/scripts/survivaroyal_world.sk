options:
  HEADER: <gray>[<dark green>SurvivaRoyal<gray>]<reset>
  SERVER_ROOT_DIR: /data
  TEMPLATE_DIR: template
  WORLD_DIR: world

variables:
  {world_execute_command} = ""
  {world_execute_param}   = ""
  
function loadWorld(player: player, command: string, param: string):
  if skutil dir "{@TEMPLATE_DIR}/%{_param}%" exists:
    broadcast "{@HEADER} World [<yellow>%{_param}%<reset>] を復元します"
    execute command "/dynmap cancelrender world"
    execute command "/mv delete {@WORLD_DIR}"
    execute command "/mv confirm"
    copy dir "{@TEMPLATE_DIR}/%{_param}%" to "/"
    rename dir "/%{_param}%" to "/world"
    execute command "/mv import world normal"
    execute command "/dynmap fullrender world"
    execute command "/wb world set 1200 1200 spawn"
    broadcast "{@HEADER} World [<yellow>%{_param}%<reset>] が復元されました"
  else:
    send "{@HEADER} World [<yellow>%{_param}%<reset>] が見つかりません" to {_player}

function saveWorld(player: player, command: string, param: string):
  if skutil dir "{@TEMPLATE_DIR}" doesn't exists:
    create directory "{@TEMPLATE_DIR}"

  if skutil dir "{@TEMPLATE_DIR}/%{_param}%" exists:
    send "{@HEADER} World [<yellow>%{_param}%<reset>] はすでに存在しています。先に<gold> /world delete <white>を実行してください" to {_player}
  else if {_param} is "world":
    send "{@HEADER} テンプレート名は <yellow>world<reset> 以外の名前にしてください" to {_player}
  else:
    broadcast "{@HEADER} World [<yellow>%{_param}%<reset>] をテンプレートとして保存します"
    copy dir "/world" to "{@TEMPLATE_DIR}/"
    broadcast "rename dir {@TEMPLATE_DIR}/world {@TEMPLATE_DIR}/%{_param}%"
    rename dir "{@TEMPLATE_DIR}/world" to "{@TEMPLATE_DIR}/%{_param}%"
    broadcast "{@HEADER} World [<yellow>%{_param}%<reset>] を保存しました"

function deleteWorld(player: player, command: string, param: string):
  if skutil dir "{@TEMPLATE_DIR}/%{_param}%" exists:
    delete dir "{@TEMPLATE_DIR}/%{_param}%"
    broadcast "{@HEADER} World [<yellow>%{_param}%<reset>] を削除しました"
  else:
    send "{@HEADER} World [<yellow>%{_param}%<reset>] は存在しません" to {_player}

function clearCommand():
  set {world_execute_command} to ""
  set {world_execute_param} to ""


#
# ルール設定コマンド
command /world [<text>] [<text>]:
  description: SurvivaRoyal World Template Manager
  permission: survivaroyal.control
  usage: /world <command> [<value>]
  trigger:
    set {_cmd} to the 1st argument
    set {_value} to the 2nd argument

    if {_cmd} is "list":
      set {_dir::*} to files in dir "{@TEMPLATE_DIR}"
      set {_templates} to ""
      loop {_dir::*}:
        set {_template} to loop-value
        replace all "{@SERVER_ROOT_DIR}/{@TEMPLATE_DIR}/" in {_template} with ""
        if skutil dir "{@TEMPLATE_DIR}/%{_template}%" exists:
          if {_templates} is "":
            set {_templates} to {_template}
          else:
            set {_templates} to "%{_templates}%, %{_template}%"

      send "{@HEADER} World: <yellow>%{_templates}%" to player

    else if {_cmd} is "confirm":
      if {world_execute_command} is "load":
        loadWorld(player, {world_execute_command}, {world_execute_param})
      else if {world_execute_command} is "save":
        saveWorld(player, {world_execute_command}, {world_execute_param})
      else if {world_execute_command} is "delete":
        deleteWorld(player, {world_execute_command}, {world_execute_param})
      else:
        if {world_execute_command} is "":
          message "{@HEADER} <light_red>コマンドが指定されていません"
        else:
          message "{@HEADER} <light_red>不正なコマンドです: <gold>%{world_execute_command}%"
      clearCommand()

    else if {_cmd} is "load":
      set {world_execute_command} to {_cmd} 
      set {world_execute_param} to {_value} 
      send "{@HEADER} <light green> 本当に World [<yellow>%{_value}%<light green>] を復元してよろしいですか?" to player
      send "{@HEADER} <light green> 問題なければ <gold>/world confirm <light green>を実行してください" to player
    else if {_cmd} is "save":
      set {world_execute_command} to {_cmd} 
      set {world_execute_param} to {_value} 
      send "{@HEADER} <light green> 本当に World [<yellow>%{_value}%<light green>] を保存してよろしいですか?" to player
      send "{@HEADER} <light green> 問題なければ <gold>/world confirm <light green>を実行してください" to player
    else if {_cmd} is "delete":
      set {world_execute_command} to {_cmd} 
      set {world_execute_param} to {_value} 
      send "{@HEADER} <light green> 本当に World [<yellow>%{_value}%<light green>] を削除してよろしいですか?" to player
      send "{@HEADER} <light green> 問題なければ <gold>/world confirm <light green>を実行してください" to player

    else:
      message "{@HEADER} Usage: world"
      message "<gold>/world list"
      message "<white>  World Template一覧の表示"
      message "<gold>/world load [template_name]"
      message "<white>  指定した Template を world として復元します"
      message "<gold>/world save [template_name]"
      message "<white>  現在の world を Template として保存します"
      message "<gold>/world delete [template_name]"
      message "<white>  指定した Template を削除します"
    
