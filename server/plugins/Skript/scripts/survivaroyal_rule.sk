options:
  HEADER: <gray>[<dark green>SurvivaRoyal<gray>]<reset>

variables:
  {rule_preset_name} = "カスタムルール"
  {rule_enable_bedcontrol} = 1
  {rule_enable_friendlyfire} = 1
  {rule_enable_nametag} = 1
  {rule_enable_strongtnt} = 1
  {rule_enable_autotnt} = 1
  {rule_enable_eggbomb} = 1
  {rule_enable_frame} = 1
  {rule_enable_hopper} = 1

function showRule():
  if {rule_preset_name} is not "":
    broadcast "{@HEADER} <gray>=== <gold>ゲームルール<white>[%{rule_preset_name}%<white>] <gray>====<reset>"
  else:
    broadcast "{@HEADER} <gray>=== <gold>ゲームルール <gray>====<reset>"

  if {rule_enable_bedcontrol} is 0:
    broadcast "{@HEADER} <white>ベッド争奪ルール ... <light red>なし<reset>"
  else:
    broadcast "{@HEADER} <white>ベッド争奪ルール ... <yellow>あり<reset>"

  if {rule_enable_friendlyfire} is 0:
    broadcast "{@HEADER} <white>フレンドリファイア ... <light red>なし<reset>"
  else:
    broadcast "{@HEADER} <white>フレンドリファイア ... <yellow>あり<reset>"

  if {rule_enable_nametag} is 0:
    broadcast "{@HEADER} <white>ネームタグ ... <light red>なし<reset>"
  else:
    broadcast "{@HEADER} <white>ネームタグ ... <yellow>あり<reset>"

  if {rule_enable_strongtnt} is not 0:
    broadcast "{@HEADER} <white>TNT威力 ... <yellow>強化<reset>"
  # else:
  #   broadcast "{@HEADER} <white>TNT威力 ... <light red>通常<reset>"

  if {rule_enable_autotnt} is not 0:
    broadcast "{@HEADER} <white>TNT自動着火 ... <yellow>あり<reset>"
  # else:
  #   broadcast "{@HEADER} <white>TNT自動着火 ... <light red>なし<reset>"

  if {rule_enable_eggbomb} is not 0:
    broadcast "{@HEADER} <white>エッグボム ... <yellow>あり<reset>"
  # else:
  #   broadcast "{@HEADER} <white>エッグボム ... <light red>なし<reset>"

  #if {rule_enable_frame} is 0:
  #  broadcast "{@HEADER} <white>アイテムフレーム ... <light red>利用不可<reset>"
  #else:
  #  broadcast "{@HEADER} <white>アイテムフレーム ... <yellow>利用可<reset>"

  #if {rule_enable_hopper} is 0:
  #  broadcast "{@HEADER} <white>ホッパー ... <light red>利用不可<reset>"
  #else:
  #  broadcast "{@HEADER} <white>ホッパー ... <yellow>利用可<reset>"


function setFriendlyFire():
  if {rule_enable_friendlyfire} is 0:
    execute command "/team modify red friendlyfire false"
    execute command "/team modify blue friendlyfire false"
    execute command "/team modify green friendlyfire false"
  else:
    execute command "/team modify red friendlyfire true"
    execute command "/team modify blue friendlyfire true"
    execute command "/team modify green friendlyfire true"

function setNameTag():
  if {rule_enable_nametag} is 0:
    execute command "/team modify red nametagVisibility never"
    execute command "/team modify blue nametagVisibility never"
    execute command "/team modify green nametagVisibility never"
  else:
    execute command "/team modify red nametagVisibility always"
    execute command "/team modify blue nametagVisibility always"
    execute command "/team modify green nametagVisibility always"

function updateRuleItem(player: player, variable: string, value: string, desc: string, desc_allow: string, desc_deny: string):
  set {rule_preset_name} to "カスタムルール"
  if {_value} is "on":
    set {%{_variable}%} to 1
    send "ルール設定 %{_desc}% ... <yellow>%{_desc_allow}%<reset>" to {_player}
  else:
    set {%{_variable}%} to 0
    send "ルール更新: %{_desc}% ... <light red>%{_desc_deny}%<reset>" to {_player}

function setRule(player: player, article: string, value: string):
  if {_article} is "bedcontrol":
    updateRuleItem({_player}, "rule_enable_bedcontrol", {_value}, "ベッド管理", "あり", "なし")
  else if {_article} is "friendlyfire":
    updateRuleItem({_player}, "rule_enable_friendlyfire", {_value}, "フレンドリファイア", "あり", "なし")
    setFriendlyFire()
  else if {_article} is "nametag":
    updateRuleItem({_player}, "rule_enable_nametag", {_value}, "ネームタグ", "あり", "なし")
    setNameTag()
  else if {_article} is "strongtnt":
    updateRuleItem({_player}, "rule_enable_strongtnt", {_value}, "TNT威力", "強化", "通常")
  else if {_article} is "autotnt":
    updateRuleItem({_player}, "rule_enable_autotnt", {_value}, "TNT自動着火", "あり", "なし")
  else if {_article} is "eggbomb":
    updateRuleItem({_player}, "rule_enable_eggbomb", {_value}, "エッグボム、エンチャント瓶ボム", "あり", "なし")
  #else if {_article} is "frame":
  #  updateRuleItem({_player}, "rule_enable_frame", {_value}, "アイテムフレーム", "利用可", "利用不可")
  #else if {_article} is "hopper":
  #  updateRuleItem({_player}, "rule_enable_hopper", {_value}, "ホッパー", "利用可", "利用不可")

function presetRule(player: player, preset: string) :: boolean:
  if {_preset} is "team":
    set {rule_preset_name} to "<light blue>サバイバロイヤル(チーム戦)"
    set {rule_enable_bedcontrol} to 1
    set {rule_enable_friendlyfire} to 0
    set {rule_enable_nametag} to 1
    set {rule_enable_strongtnt} to 1
    set {rule_enable_autotnt} to 0
    set {rule_enable_eggbomb} to 1

  else if {_preset} is "team-notag":
    set {rule_preset_name} to "<light blue>名無しサバロワ(チーム戦)"
    set {rule_enable_bedcontrol} to 1
    set {rule_enable_friendlyfire} to 0
    set {rule_enable_nametag} to 0
    set {rule_enable_strongtnt} to 1
    set {rule_enable_autotnt} to 0
    set {rule_enable_eggbomb} to 1

  else if {_preset} is "battleroyal":
    set {rule_preset_name} to "<light green>バトルロワイヤル(個人戦)"
    set {rule_enable_bedcontrol} to 0
    set {rule_enable_friendlyfire} to 1
    set {rule_enable_nametag} to 0
    set {rule_enable_strongtnt} to 1
    set {rule_enable_autotnt} to 0
    set {rule_enable_eggbomb} to 0

  else if {_preset} is "tntroyal":
    set {rule_preset_name} to "<light red>TNTロワイヤル<light green>(個人戦)"
    set {rule_enable_bedcontrol} to 0
    set {rule_enable_friendlyfire} to 1
    set {rule_enable_nametag} to 0
    set {rule_enable_strongtnt} to 1
    set {rule_enable_autotnt} to 1
    set {rule_enable_eggbomb} to 0
  else:
    send "不正なルール名です: %{_preset}%" to {_player}
    return false

  setNameTag()
  setFriendlyFire()
  return true


#
# ルール設定コマンド
command /rule [<text>] [<text>] [<text>]:
  description: SurvivaRoyal Game Rule
  permission: survivaroyal.control
  usage: /rule <command> [on/off]
  trigger:
    set {_cmd} to the 1st argument
    set {_value} to the 2nd argument

    if {_cmd} is "show":
      showRule()
    else if {_cmd} is "set":
      setRule(player, the 2nd argument, the 3rd argument)
      showRule()
    else if {_cmd} is "preset":
      if presetRule(player, the 2nd argument) is true:
        showRule()
    else:
      message "{@HEADER} Usage: rule"
      message "<gold>/rule show"
      message "<white>  現在のルールをブロードキャストします。"
      message "<gold>/rule preset [team | team-notag | battleroyal | tntroyal]"
      message "<white>  プリセットルールを適用します。"
      message "<gold>/rule set <article> [on | off]"
      message "<white>  ルールを個別に設定します。"
      message "<gold>  article: <yellow>bedcontrol, friendlyfire, nametag, strongtnt, autotnt, eggbomb"

    if {survivaroyal_game_running} is 1:
      message "<light red>※ゲーム進行中です。ルール変更には十分注意してください<reset>"

#
# Item frameクラフト禁止

on craft of a item frame:
  if {rule_enable_frame} is 0:
    message "{@HEADER} <light red>アイテムフレームのクラフトは禁止されています<reset>"
    cancel event

on place of a item frame:
  if {rule_enable_frame} is 0:
    message "{@HEADER} <light red>アイテムフレームの設置は禁止されています<reset>"
    cancel event


#
# Hopperクラフト禁止

on craft of a hopper:
  if {rule_enable_hopper} is 0:
    message "{@HEADER} <light red>ホッパーのクラフトは禁止されています<reset>"
    cancel event

on place of a hopper:
  if {rule_enable_hopper} is 0:
    message "{@HEADER} <light red>ホッパーの設置は禁止されています<reset>"
    cancel event

#
# Bedクラフト禁止

on craft of any bed:
  if {rule_enable_bedcontrol} is 1:
    message "{@HEADER} <light red>ベッドのクラフトは禁止されています<reset>"
    cancel event


#
# TNT強化

on explosion:
  entity is tnt:
    if {rule_enable_strongtnt} is 1:
      make an explosion with power 6 at entity

#
# TNT自動着火・スニークで無着火

on place of tnt:
  if {rule_enable_autotnt} is 1:
    set event-block to air
    spawn primed tnt at event-location

on rightclick:
  if clicked block is tnt:
    if {rule_enable_autotnt} is 1:
      if player is not sneaking:
        set event-block to air
        spawn primed tnt at event-location
        cancel event

#
# エッグボム・エンチャントボトルボム  
on projectile hit:
  if {rule_enable_eggbomb} is 1:
    if projectile is bottle of enchanting:
      create explosion of power 3 at the projectile 
    if projectile is egg:
      create explosion of power 1 at the projectile 


#
# 各プレイヤーのコンパスのターゲットを最も近いベッドに設定する

every 5 seconds:
  if {rule_enable_bedcontrol} is 1:
    loop all players:
      set {_distance} to 99999
      delete {_target}

      if {bed.red} is set:
        set {_d_red} to distance between the loop-player and {bed.red} 
        if {_distance} is larger than {_d_red}:
          set {_distance} to {_d_red}
          set {_target} to {bed.red}
        
      if {bed.green} is set:
        set {_d_green} to distance between the loop-player and {bed.green}
        if {_distance} is larger than {_d_green}:
          set {_distance} to {_d_green}
          set {_target} to {bed.green}
        
      if {bed.blue} is set:
        set {_d_blue} to distance between the loop-player and {bed.blue}
        if {_distance} is larger than {_d_blue}:
          set {_distance} to {_d_blue}
          set {_target} to {bed.blue}

      if {_target} is set:
        set the loop-player's compass target to {_target}

      #message "%location of loop-player% %{_distance}% %{bed.red}% %{_target}%" to console 

#
# ベッドの範囲指定、設置チームの記録

on place of any bed:
  if {rule_enable_bedcontrol} is 1:
    # 自チームのベッドが既に存在している場合、警告
    if {bed.%{team.%player%}%} is set:
      if player has permission "survivaroyal.control":
        message "{@HEADER} <yellow>チームベッドを上書きしました。古いベッドが残っている可能性があります。%{bed.%{team.%player%}%}%<reset>"
      else:
        message "{@HEADER} <yellow>チームベッドは既に設置済みです。<reset>"
        cancel event

    # 各チームのメンバーが置いたベッドの座標を覚えておく
    set {bed.%{team.%player%}%} to block's location


#
# Bedを拾ったことを周知する

on pick up of any bed:
  if {rule_enable_bedcontrol} is 1:
    set {_player} to player
    set {_team} to {team.%player%}

    message "{@HEADER} <yellow>ベッドを拾いました！<reset>"
    execute command "/tm %{_team}% <yellow>%{_player}%がベッドを拾いました！<reset>"


#
# ベッドの破壊判定

on break of any bed:
  if {rule_enable_bedcontrol} is 1:
    set {_player} to player
    set {_team} to {team.%player%}
    broadcast "{@HEADER} <yellow>どこかでベッドが破壊されました<reset>"

    #if damage was caused by block explosion:
    #  message "{@HEADER} TNT!TNT!"

    distance between block's location and {bed.red} is smaller than 2:
      delete {bed.red}
      {_team} is not "red":
        execute command "/tm %{_team}% %{_player}%が<light red>赤チーム<reset>のベッドを破壊した！"
    distance between block's location and {bed.green} is smaller than 2:
      delete {bed.green}
      {_team} is not "green":
        execute command "/tm %{_team}% %{_player}%が<light green>緑チーム<reset>のベッドを破壊した！"
    distance between block's location and {bed.blue} is smaller than 2:
      delete {bed.blue}
      {_team} is not "blue":
        execute command "/tm %{_team}% %{_player}%が<light blue>青チーム<reset>のベッドを破壊した！"

on burn of any bed:
  if {rule_enable_bedcontrol} is 1:
    broadcast "{@HEADER} <yellow>どこかでベッドが燃えました<reset>"

#
# リスポーン時にベッドがなければチームから抜ける

on respawn:
  wait 1 tick
  if {rule_enable_bedcontrol} is 1:
    if {team.%player%} is set:
      if {bed.%{team.%player%}%} is not set:
        execute command "/srleave %player%"
